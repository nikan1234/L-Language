options {
  STATIC = false;
  MULTI = true;
  NODE_DEFAULT_VOID=true;
  NODE_PREFIX = "";
}

PARSER_BEGIN(LStatement)
package ru.nsu.logic.lang.grammar;

import java.util.*;
import ru.nsu.logic.lang.base.*;
public class LStatement {}
PARSER_END(LStatement)

SKIP : {
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN : {
  < CLASS: "class">
  |
  < FUNCTION: "function">
  |
  < INHERIT: "extends">
  |
  < END: ";">
  |
  < OBR: "{" >
  |
  < CBR: "}" >
  |
  < OPAR  : "(" >
  |
  < CPAR  : ")" >
  |
  < COMMA: "," >
  |
  < DOT: "." >
  |
  < ASSIGN: ":=">
  |
  < THIS: "this">
  |
  < RETURN: "return">
  |
  < ACCESS_PUBLIC: "public">
  |
  < ACCESS_PRIVATE: "private">
  |
  < ACCESS_PROTECTED: "protected">
  |
  < Identifier: ["a"-"z","A"-"Z", "_"] ( ["a"-"z","A"-"Z","0"-"9","_"] )* >
  |
  < BinaryOp: ["+", "-", "*", "/"] >
  |
  < IntVal: ( ["0"-"9"] )+ >
  |
  < FloatVal: ( ["0"-"9"] )+ "." ( ["0"-"9"] )+  >
  |
  < StrVal: "\"" (~["\""])* "\"" >
}

/*-----------------Common-----------------*/

AccessTypeEnum AccessType() :
{Token t;}
{
  (t=<ACCESS_PUBLIC> | t=<ACCESS_PRIVATE> | t=<ACCESS_PROTECTED>)
  {return AccessTypeEnum.valueOf(t.image.toUpperCase());}
}

String Identifier() :
{Token t;}
{
  t=<Identifier> {return t.image;}
}


/*-----------------Declarations-----------------*/

void ProgramDeclaration():
{}
{
  FunctionDeclaration() | ClassDeclaration()
}

/*-----------------Function or Method commons-----------------*/

String ArgumentDeclaration() # ArgumentDeclaration :
{String arg;}
{
  arg=Identifier()
  {jjtThis.jjtSetValue(arg);return arg;}
}

/* ---------------STATEMENT -------------------------*/

IntegerValue IntValue() #IntegerValue :
{Token t;}
{
  t=<IntVal> {long v = Long.valueOf(t.image); jjtThis.setValue(v); return jjtThis;}
}

FloatValue FloatValue() #FloatValue :
{Token t;}
{
  t=<FloatVal> {double v = Double.valueOf(t.image); jjtThis.setValue(v); return jjtThis;}
}

MemberStatement MemberStatement() # MemberStatement :
{String memberName;}
{
  <THIS> <DOT> memberName=Identifier()
  {jjtThis.setName(memberName); return jjtThis;}
}

VariableStatement VariableStatement() # VariableStatement :
{String varName;}
{
  varName=Identifier()
  {jjtThis.setName(varName); return jjtThis;}
}

FunctionCallStatement FunctionCallStatement() # FunctionCallStatement:
{String calledFuncName; IStatement param; List<IStatement> params = new ArrayList();}
{
   calledFuncName=Identifier()
   <OPAR>
      (param=rvalueStatement()         {params.add(param);}
      (<COMMA> param=rvalueStatement() {params.add(param);} )*)?
   <CPAR>
   {
      jjtThis.setFunctionName(calledFuncName);
      jjtThis.setCallParameters(params);
      return jjtThis;
    }
}

IStatement SingleStatement() :
{IStatement retVal;}
{
  (
    LOOKAHEAD(2) retVal = FunctionCallStatement() |
    retVal=MemberStatement() |
    retVal=VariableStatement() |
    retVal=IntValue() |
    retVal=FloatValue()
  )
  {return retVal;}
}

ComplexStatement ComplexStatement() # ComplexStatement :
{IStatement lhs = null; IStatement rhs = null; Token op = null;
 List<IStatement> args = new ArrayList();
 List<String> ops = new ArrayList();
}
{ (               (lhs=SingleStatement() | <OPAR> lhs=ComplexStatement() <CPAR>) {args.add(lhs);}
   (op=<BinaryOp> (rhs=SingleStatement() | <OPAR> rhs=ComplexStatement() <CPAR>) {args.add(rhs); ops.add(op.image);})*)
  {jjtThis.setOperands(args); jjtThis.setOperators(ops); return jjtThis;}
}

IStatement rvalueStatement():
{IStatement retVal;}
{
  (LOOKAHEAD((<OPAR> ComplexStatement() <CPAR> | SingleStatement()) <BinaryOp>)
   retVal = ComplexStatement()
   |
   retVal = SingleStatement())
  {return retVal;}
}

AssignmentStatement AssignmentStatement() # AssignmentStatement :
{IStatement target; IStatement what;}
{
   (target=MemberStatement() | target=VariableStatement())
   <ASSIGN> what = rvalueStatement()
   {jjtThis.setTarget(target); jjtThis.setWhat(what); return jjtThis;}
}

void ReturnStatement() # ReturnStatement :
{}
{ <RETURN> rvalueStatement() }

void AllStatements() :
{}
{ (LOOKAHEAD(2) AssignmentStatement() | FunctionCallStatement() | ReturnStatement()) <END> }


/*-----------------Class-----------------*/

void ClassDeclaration() #ClassDeclaration :
{String className; String baseClass = null;}
{
  <CLASS> className=Identifier() [baseClass=InheritanceDeclaration()]
  <OBR> (LOOKAHEAD(2) ClassMemberDeclaration() | ClassMethodDeclaration())*
  <CBR>
  {jjtThis.setClassName(className);
   jjtThis.setBaseClass(baseClass);}
}

String InheritanceDeclaration() :
{String className;}
{
  <INHERIT> className=Identifier()
  {return className;}
}

void ClassMemberDeclaration() #ClassMemberDeclaration :
{AccessTypeEnum access; String member;}
{
  access=AccessType() member=Identifier() <END>
  {jjtThis.setAccessType(access);jjtThis.setName(member);}
}

void ClassMethodDeclaration() #ClassMethodDeclaration :
{AccessTypeEnum access = AccessTypeEnum.PROTECTED; String methodName;}
{
  [access = AccessType()] <FUNCTION> methodName=Identifier()
  <OPAR> (ArgumentDeclaration() (<COMMA> ArgumentDeclaration())*)? <CPAR>
  <OBR> (AllStatements())* <CBR>
  {jjtThis.setAccessType(access);
   jjtThis.setName(methodName);}
}

/*-----------------Function-----------------*/

void FunctionDeclaration() #FunctionDeclaration :
{String functionName;}
{
  <FUNCTION> functionName=Identifier()
  <OPAR> (ArgumentDeclaration() (<COMMA> ArgumentDeclaration())*)? <CPAR>
  <OBR> (AllStatements())* <CBR>
  {jjtThis.setName(functionName);}
}

/* -----------------Program-----------------*/
LLangProgram LLangProgram() #LLangProgram :
{}
{
  (ProgramDeclaration() | AllStatements())* <EOF>
   {return jjtThis;}
}